buildscript {
    ext.kotlin_version = '1.2.31'
    ext.bouncycastleVersion = '1.54'

    repositories {
        mavenCentral()
        maven {
            name 'JFrog OSS snapshot repo'
            url  'https://oss.jfrog.org/oss-snapshot-local/'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'me.champeau.gradle:antlr4-gradle-plugin:0.1.1-SNAPSHOT'
    }
}

group 'io.github.ghostbuster91'
version '1.0-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'me.champeau.gradle.antlr4'

antlr4 {
    source = file('src/main/antlr')
    output = file('src/main/java/io/github/ghostbuster91/ethereum/sha3/parser')
    extraArgs = ['-package', 'io.github.ghostbuster91.ethereum.sha3.parser']
}
compileJava.dependsOn antlr4
sourceCompatibility = 1.8

sourceSets.main.java.srcDirs += antlr4.output
//sourceSets.main.kotlin.srcDirs += antlr4.output

repositories {
    mavenCentral()
}

configurations {
    compile.extendsFrom antlr4
}


dependencies {
    compile "com.xenomachina:kotlin-argparser:2.0.7"
    compile ('org.web3j:core:3.4.0')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.bouncycastle:bcprov-jdk15on:$bouncycastleVersion"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Main-Class': 'io.github.ghostbuster91.ethereum.sha3.MainKt'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}